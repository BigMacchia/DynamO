/*  DYNAMO:- Event driven molecular dynamics simulator 
    http://www.marcusbannerman.co.uk/dynamo
    Copyright (C) 2010  Marcus N Campbell Bannerman <m.bannerman@gmail.com>

    This program is free software: you can redistribute it and/or
    modify it under the terms of the GNU General Public License
    version 3 as published by the Free Software Foundation.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <http://www.gnu.org/licenses/>.
*/
#pragma once

#define STRINGIFY(A) #A
const std::string sphereKernelSource = STRINGIFY(
__kernel void
SphereRenderKernel(const __global float4* positions,
		   __global float * vertexBuffer,
		   const __global float * vertices,
		   const uint nVertex,
		   const uint StartParticle,
		   const uint EndParticle,
		   const int VertexOffset,
		   __global const uint* sortedArray)
{
  //We're trying to coalesce the reads and writes from memory, so we cache
  __local float4 positionCache[WORKGROUP_SIZE];
  __local const float* positionCacheComponents = (__local const float*)positionCache;

  //Iterate on blocks of WORKGROUP_SIZE, with a stride of the global_size
  for (uint id0 = get_group_id(0) * WORKGROUP_SIZE + StartParticle;
       id0 < EndParticle; 
       id0 += get_global_size(0))
    {
      //Load the cache
      uint data_to_read = id0 + get_local_id(0);
      
      //Coalesced read from global mem and conflict-free localsss write
      if (data_to_read < EndParticle)
	{
	  data_to_read = sortedArray[data_to_read];
	  positionCache[get_local_id(0)] = positions[data_to_read];
	}
      
      barrier(CLK_LOCAL_MEM_FENCE);
      
      //Cache is full, now start blitting out verticies
      //Calculate the number of verticie components to do
      uint particles_InCache = min((uint)(WORKGROUP_SIZE), EndParticle - id0);
      uint vertices_components_to_do = 3 * particles_InCache * nVertex;
      
      for (uint vertex_component = get_local_id(0);
	   vertex_component < vertices_components_to_do;
	   vertex_component += get_local_size(0))
	{
	  uint particleID = 4 * (vertex_component / (3 * nVertex));
	  uint vertexID = vertex_component % (3 * nVertex);
	  
	  vertexBuffer[VertexOffset + 3 * nVertex * id0 + vertex_component]
	    = positionCacheComponents[particleID  + (vertex_component % 3)] 
	    + positionCacheComponents[particleID  + 3] * vertices[vertexID];
	}

      barrier(CLK_LOCAL_MEM_FENCE);
    }
}

float frustrumPenalty(const float4 plane, const float4 pos)
{ 
  float offset = dot(plane,pos) + pos.w;
  return (offset < 0) ? 10 + offset * offset : 0;
}

__kernel void
GenerateData(const __global float4* positions,
	     __global float* keys,
	     __global uint* data,
	     float4 camPos, float4 camdir, float4 camup,
	     float ratio, float nearDist, float fov,
	     uint N, uint paddedN)
{
  //Need to add the aspect ratio!
  if (get_global_id(0) >= paddedN) return;
  
  if (get_global_id(0) >= N)
    {
      keys[get_global_id(0)] = MAXFLOAT;
      data[get_global_id(0)] = -1;
      return;
    }

  float tanfactor = (nearDist * tan(fov * M_PI_F / 360.0f));

  float4 left = cross(camup, camdir);

  float penalty = 0;

  float4 pos = positions[get_global_id(0)];
  pos -= camPos;
  //The 4th component of the position contains the radius of the sphere
  
  //First calculate the view distance penalty
  {
    float distance = dot(pos, camdir);
    penalty += (distance < 0) ? 10000 : distance / pos.w;

    float centerdistance = length(pos - distance * camdir);
    penalty += 2 * centerdistance / pos.w;
  }

  //Frustrum Penalties
  {
    float4 topPoint = camdir * nearDist + camup * tanfactor;
    
    topPoint /= length(topPoint);
    
    float4 topNorm = cross(left,topPoint);
    
    penalty += frustrumPenalty(topNorm, pos);
  }

  {
    float4 bottomPoint = camdir * nearDist - camup * tanfactor;
    
    bottomPoint /= length(bottomPoint);
    
    float4 bottomNorm = cross(-left,bottomPoint);

    penalty += frustrumPenalty(bottomNorm, pos);
  }

  tanfactor *= ratio;

  {
    float4 leftPoint = camdir * nearDist + left * tanfactor;
    
    leftPoint /= length(leftPoint);
    
    float4 leftNorm = cross(-camup, leftPoint);

    penalty += frustrumPenalty(leftNorm, pos);
  }

  {
    float4 rightPoint = camdir * nearDist - left * tanfactor;
    
    rightPoint /= length(rightPoint);
    
    float4 rightNorm = cross(camup,rightPoint);
    
    penalty += frustrumPenalty(rightNorm, pos);
  }

  keys[get_global_id(0)] = penalty;
  data[get_global_id(0)] = get_global_id(0);
}
);
#undef STRINGIFY
